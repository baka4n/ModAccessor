plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'groovy-gradle-plugin'
    id 'com.gradleup.shadow' version '8.3.6'
    id "com.gradle.plugin-publish" version "1.3.1"
}

group = 'dev.vfyjxf.gradle'
version = '1.2.1'

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
    maven { url = 'https://maven.neoforged.net/releases' }
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    compileOnly gradleApi()
    shade "org.apache.commons:commons-lang3:3.8.1"
    shade "com.google.code.gson:gson:2.10"
    shade "org.ow2.asm:asm-util:9.7"
//    shade "net.neoforged.jst:jst-cli-bundle:1.0.72"
    shade("net.neoforged:accesstransformers:11.0.2") {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOnly('org.slf4j:slf4j-api:2.0.9')
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}
shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.shade]
    relocate 'net.neoforged.jst', 'dev.vfyjxf.gradle.accessor.relocate.jst'
    relocate 'net.neoforged.accesstransformers', 'dev.vfyjxf.gradle.accessor.relocate.accesstransformers'
    relocate 'org.objectweb.asm', 'dev.vfyjxf.gradle.accessor.relocate.asm'
    relocate 'org.apache.commons.lang3', 'dev.vfyjxf.gradle.accessor.relocate.commons.lang3'
}

gradlePlugin {
    website = 'https://github.com/vfyjxf/ModAccessor'
    vcsUrl = 'https://github.com/vfyjxf/ModAccessor'
    plugins {
        mod_accessor {
            displayName = 'ModAccessor'
            description = "A simple gradle plugin to solve the problem of accessing private fields and methods during compile time."
            id = 'dev.vfyjxf.modaccessor'
            tags.addAll('neoforge', 'minecraft', 'java')
            implementationClass = 'dev.vfyjxf.gradle.accessor.ModAccessorPlugin'
        }
    }
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact shadowJar
            groupId = project.group
            artifactId = project.name
            version = project.version
        }
    }
}